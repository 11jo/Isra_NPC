// --------------------------------------------
// Basics
BACKUP ~isra/backup~
AUTHOR ~Rhaella~
VERSION ~v2.3~
README ~Isra/isra_readme.html~


ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET is_tutu BEGIN // check to make this happen only once per install

	/* -------------------------- *
	 *    Sod: Modmerge check.    *
	 * -------------------------- */
		ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) BEGIN FAIL @21 END


	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
		OUTER_SET is_tutu = (GAME_IS ~tutu tutu_totsc~ ? 1 : 0)						// tutu
		OUTER_SET is_bgt = (GAME_IS ~bgt~) ? 1 : 0									// bgt
		OUTER_SET is_totsc = (GAME_IS ~tutu_totsc bgt bgee eet~) ? 1 : 0			// TotSC

		OUTER_SET is_bgee = (GAME_IS ~bgee~ AND NOT GAME_INCLUDES ~sod~) ? 1 : 0	// bgee without SoD
		OUTER_SET is_sod = (GAME_IS ~bgee~ AND GAME_INCLUDES ~sod~) ? 1 : 0			// SoD
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0									// eet
		OUTER_SET is_ee = (GAME_IS ~bgee eet~) ? 1 : 0								// EE game


	/* ------------------------------------------------------------------------- *
	 *    Loads Crossplatform variables according to which game is installed.    *
	 * ------------------------------------------------------------------------- */
		ACTION_IF is_tutu BEGIN
			INCLUDE ~isra/lib/g3_tutu_cpmvars.tpa~
			PRINT @22	// ~Tutu install detected.~
		END
		ACTION_IF is_bgt BEGIN
			INCLUDE ~isra/lib/g3_bgt_cpmvars.tpa~
			PRINT @23	// ~BGT install detected.~
		END
		ACTION_IF GAME_IS ~bgee~ BEGIN
			INCLUDE ~isra\lib\liam_bgee_vars.tpa~
			PRINT @24	// ~Baldur's Gate Enhanced Edition install detected.~
		END
		ACTION_IF is_eet BEGIN
			INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
			PRINT @25	// ~Enhanced Edition Trilogy install detected.~
		END
		INCLUDE ~isra_cpmvars.tpa~


	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
	 *    No exception, everything needs to be converted                                             *
	 *    setup.tra contains in game texts as well as installation texts and need to be converted    *
	 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY rh#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY rh#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/translations~
				noconvert_array = rh#noconvert
				reload_array = rh#reload
		END

	END

END


AUTO_TRA ~isra/translations/%s~

LANGUAGE ~English~
         ~english~
         ~isra/translations/english/setup.tra~


/* =========================================== *
 *  Isra NPC Mod for BG:EE, EET, BGT and TuTu  *
 * =========================================== */
BEGIN @1													 /* Isra NPC Mod for BG:EE, EET, BGT and TuTu */
DESIGNATED 0 LABEL ~isra_npc_for_bg~
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgee eet bgt~ @11 /* You must have Tutu, BGT, BG:EE or EET installed to install Isra NPC for BG mod. */


/* =============================== *
 *  Valerie/Isra crossmod content  *
 * =============================== */
BEGIN @2										/* Valerie/Isra crossmod content */
DESIGNATED 1 LABEL ~isra_valerie_crossmod_content~
REQUIRE_COMPONENT ~isra.tp2~ ~0~ @12			/* Please install Isra NPC for BG main component before additional content. */
REQUIRE_COMPONENT ~valerie.tp2~ ~0~ @13			/* You do not appear to have Valerie installed. */
  
COMPILE EVALUATE_BUFFER ~isra/crossmod/valerie.d~


/* ============================= *
 *  Gavin/Isra crossmod content  *
 * ============================= */
BEGIN @3										/* Gavin/Isra crossmod content */
DESIGNATED 2 LABEL ~isra_gavin_crossmod_content~
REQUIRE_COMPONENT ~isra.tp2~ ~0~ @12			/* Please install Isra NPC for BG main component before additional content. */
REQUIRE_COMPONENT ~gavin.tp2~ ~0~ @14			/* You do not appear to have Gavin installed. */
  
COMPILE EVALUATE_BUFFER ~isra/crossmod/gavin.d~
EXTEND_BOTTOM ~b!gavin.bcs~ ~isra/crossmod/gavin.baf~
